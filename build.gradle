plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.flyerzrule.mc.customtags'
version = '1.4'
def javaVersion = JavaVersion.VERSION_17
def encoding = 'UTF-8'

repositories {
    gradlePluginPortal()
    mavenCentral()
    maven {
        name 'papermc-repo'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name 'xenondevs'
        url 'https://repo.xenondevs.xyz/releases'
    }
    maven { 
        url 'https://jitpack.io' 
    }
    maven {
        url = "https://maven.pkg.github.com/KillionRevival/KillionCommons"
        credentials {
            username = findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
    implementation 'xyz.xenondevs.invui:invui:1.33'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'co.killionrevival:killioncommons:1.1.0'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
    compileOnly 'net.md-5:bungeecord-chat:1.20-R0.2'
    compileOnly 'net.luckperms:api:5.4'
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile) {
    options.encoding = encoding
}

tasks.processResources {
    filesMatching('plugin.yml') {
        expand version: project.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'customtags'
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/KillionRevival/CustomTags')
            credentials {
                username = findProperty("gpr.user") ?: System.getenv('GITHUB_ACTOR')
                password = findProperty("gpr.key") ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

